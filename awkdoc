#!/usr/bin/awk -f
# vim: ft=awk tw=80 sw=3 ts=3 sts=3

BEGIN {
   # Color escapes. For pretty-printing debugging & error information.
   c["rst"] = "\033[0m"
   c["rd"]  = "\033[31;1m"
   c["yl"]  = "\033[33;1m"
   c["cy"]  = "\033[35m"

   # Convenience variables to make boolean assignments more clear.
   false = 0
   true  = 1

   # Upon hitting an error, "panic" until the start of a next valid opt.
   panicking            = false
   opts["@arg"]         = true
   opts["@env"]         = true
   opts["@set"]         = true
   opts["@type"]        = true
   opts["@noargs"]      = true
   opts["@section"]     = true
   opts["@description"] = true

   # "Declaring" as an array, via witchcraft.
   delete _ERRORS[0]
   delete _TYPEDEFS[0]
   delete _ENV[0]
   delete _SET[0]

   column = 0
   buffer = ""

   # gensub() format strings.
   md["h1"]   = "# &"
   md["h2"]   = "## &"
   md["h3"]   = "### &"
   md["h4"]   = "#### &"
   md["h5"]   = "##### &"
   md["link"] = "[`&`](#&)"
   md["li1"]  = "- &"
   md["li2"]  = "  - &"

   # Links to format strings.
   md["toc"]     = md["h1"]
   md["types"]   = md["h1"]
   md["body"]    = md["h1"]
   md["section"] = md["h2"]
   md["fn"]      = md["h3"]
   md["sub"]     = md["h4"]

   _TYPE = ""
   _TOC  = _gensub("Contents.", md["toc"])
   _BODY = _gensub("Functions.", md["body"])
}


function reset() {
   column      = 0
   buffer_open = false
   buffer      = ""
   section     = ""

   delete fn
}


#-------------------------------------------------------------------------------
# @section                      I/O & debugging

# significant()
# @description
#  Is log $level "significant" enough to warrant printing to stderr. Allows user
#  to ignore INFO, DEBUG, (or ERROR if they're making bad decisions) messages.
#
# @env   AWKDOC_LOG_LEVEL
# @arg   level    Message log level: ["ERROR", "INFO", "DEBUG"]
# @local l        Numeric representation of levels --^
# @local s        Numeric log level from $AWKDOC_LOG_LEVEL
function significant(level,  l,s) {
   if (level == "ERROR") l = 0
   if (level == "INFO")  l = 1
   if (level == "DEBUG") l = 2

   s = ENVIRON["AWKDOC_LOG_LEVEL"]
   if (s == "") s = 0

   return l <= s
}


# color()
# @description
#  Applies color to the log $level.
#
# @arg   level    Message log level: ["ERROR", "INFO", "DEBUG"]
function color(level) {
   if (ENVIRON["AWKDOC_NO_COLOR"]) {
      return level
   }

   if (!level) level = "INFO"
   if (level == "ERROR") return c["rd"] level c["rst"]
   if (level == "INFO")  return c["cy"] level c["rst"]
   if (level == "DEBUG") return c["cy"] level c["rst"]

   return level
}


# context()
# @description
#  Builds file & line context information for error reporting. Example, given
#  the line...
#  ```sh
#  # @section
#  ```
#
#  ...returns the following
#  ```
#  in <filename>
#  ln. <line number>: $line
#  ```
#
# @arg   line     Original full line of text from the source file
function context(line) {
   line = sprintf("%4sln. %s: %s\n", "", FNR, line)
   line = sprintf("%4sin %s\n", "", FILENAME) line
   return line 
}


# log_error()
# @description
#  Does two things:
#  - Adds the formatted error message to global _ERRORS[]
#  - Enables "panic mode", skipping all text until the next valid annotation
#    flag
#  
# @arg   line     Original full line of text from the source file
# @arg   text     Error message text ("missing section title", etc)
# @local level    Error log level is always "ERROR"
function log_error(line, text,   level) {
   level = "ERROR"
   text  = "==> " color(level) ": " text
   text  = text "\n" context(line)

   if (significant(level)) {
      push(_ERRORS, text)
   }

   panicking = true
}


# write()
# @description
#  Like `log_error()`, except prints to stderr instead of appending to
#  _ERRORS[].
#
# @see   log_errors
#
# @arg   line     Original full line of text from the source file
# @arg   text     Error message text ("missing section title", etc)
# @arg   level    Message log level: ["ERROR", "INFO", "DEBUG"]
function write(line, text, level) {
   text  = "==> " color(level) ": " text
   text  = text "\n" context(line)

   if (significant(level)) {
      print text > "/dev/stderr"
   }
}


# typedef_in_function()
# @description
#  Throws error if user has a `@type` flag attached to function's annotation.
#
# @arg   line     Original full line of text from the source file
function typedef_in_function(line) {
   write(line, "Cannot attach @type flag to this function.", "INFO")
}


# section_in_function()
# @description
#  Throws error if user has a `@section` flag attached to function's annotation.
#
# @arg   line     Original full line of text from the source file
# @local msg      Error message text
function section_in_function(line,   msg) {
   msg = "Cannot attach @section to this function. Did you intend to add a line"
   msg = msg sprintf("\n%10sbreak after the section flag?", "")
   write(line, msg, "INFO")
}


# noargs_args_mismatch()
# @description
#  Throws error if user has both `@noargs` and `@args` flags attached to a
#  function's annotation.
#
# @arg   line     Original full line of text from the source file
function noargs_args_mismatch(line) {
   log_error(line, "@noargs and @arg flags are mutually exclusive.")
}


#-------------------------------------------------------------------------------
# @section                     text manipulation

# shift()
# @description
#  Pops the first word of the current line. Removes preceding whitespace before
#  and after the .pop()
#
# @local name     First "word" of $0
function shift(   name) {
   name = $1
   sub(/^[[:space:]]*[^[:space:]]*[[:space:]]*/, "")
   return name
}


# push()
# @description    It's array.push()
#
# @arg   arr      Array to push $val to
# @arg   val      Value to push to $arr
# @local i        Index of array while iterating
function push(arr, val,   i) {
   for (i=0 ;; ++i) {
      if (! arr[i]) {
         arr[i]=val
         break
      }
   }
}


# cat()
# @description    Concatenates left & right strings, adds "\n" between.
function cat(left, right) {
   if (left) {
      left = left "\n" right
   } else {
      left = right
   }
   return left
}


# empty()
# @description    Returns bool if array is empty
function empty(arr) {
   for (i in arr) return false
   return true
}


# _gensub()
# @description    Dumb wrapper around `gensub()`, supplying common args.
function _gensub(str, repl) {
   return gensub(".*", repl, 1, str)
}


#-------------------------------------------------------------------------------
# @section                        build output

# link()
# @description
#  Creates markdown anchor, optionally formatting text to monospace.
#
# @arg   text     Anchor name & location (will always be the same)
# @arg   mono     [Optional] if present, anchor name will be made monospace
# @local _text    Formatted monospace text
function link(text, mono,   _text) {
   _text = text

   text = tolower(text)
   gsub(/[^[:alnum:][:space:]_]/, "", text)
   gsub(/\s/, "-", text)

   if (mono) {
      _text = "`" _text "`"
   }

   text = "[" _text "](#" text ")"
   return text
}


# add_type()
# @description
#  Does two things:
#  - Adds the type $name to global _TYPE section
#  - Adds the type $name to _TYPEDEF{}, allowing creating anchors if the type
#    shows up in function argument annotations
#
# @see   match: @arg
# @see   add_arg
# @env   _TYPE
# @env   _TYPEDEFS
#
# @arg   name     Type's name
# @local t        Monospaced type name
# @local l        Monospaced line number
# @local f        Monospaced file name
function add_type(name,   t,l,f) {
   if (!_TYPE) {
      _TYPE = _gensub("Types.", md["types"])
      _TYPE = cat(_TYPE, "|type|line|file|")
      _TYPE = cat(_TYPE, "|:---|:---|:---|")
   }

   _TYPEDEFS[name] = true

   t = "`" name     "`"
   l = "`" FNR      "`"
   f = "*" FILENAME "*"
   _TYPE = cat(_TYPE, "|" t "|" l "|" f "|" )
}


# add_section()
# @description
#  Adds declared section name (and optional description) to the
#  - Table of contents
#  - H2 heading under [#functions]
#
# @SET   _BODY
# @SET   _TOC
#
# @local s        Formatted section H2 heading
# @local t        Formatted section in TOC
function add_section(   s,t) {
   s = _gensub(section, md["section"])
   if (buffer) {
      s = cat(s, buffer "\n")
   }

   t = gensub(".*", md["li1"], 1, link(section))

   _BODY = cat(_BODY, s)
   _TOC  = cat(_TOC,  t)
}


# add_arg()
# @description
#  Adds an argument name, optional type/description to the currently annotated
#  function. If the type was previously declared as a typedef, an anchor is
#  created to the [#types] section. Returns result formatted for a markdown
#  table.
#
# @see   add_type
#
# @arg   name     Argument's name
# @arg   type     [Optional] type, prefixed by `:`
# @arg   desc     [Optional] oneline argument description
# @local arg      Formatted argument table row
# @local _type    Formatted type column, perhaps containing anchor
function add_arg(name, type, desc,   arg, _type) {
   gsub(/\|/, "\\|", desc)

   if (type) {
      _type = "`" type "`"
   }

   if (_TYPEDEFS[type]) {
      _type = "[" _type "](#types.)"
   }

   arg = "|`" name  "`|"  _type  "|"  desc  "|"
   push(fn["args"], arg)
}


# add_fn()
# @description
#  Oof. Does many things.
#  - Ignores & returns when:
#    - There's no function annotations
#    - User passes `@internal` flag
#  - Throws errors when:
#    - User passes `@section` flag attached to function
#    - User passes `@type` flag attached to function
#    - User passes both `@noargs` and an `@arg` flag
#  - Appends function to appropriate _{SET,ENV} array(s)
#  - Adds function to table of contents
#  - Adds H3 heading and its formatted arg/env/set components
#
#  Sounds like this should be split into a couple separate functions, hmm.
#
# @set   _SET
# @set   _ENV
# @set   _BODY
# @set   _TOC
#
# @arg   name     Function's name
# @arg   line     Original full line of text from the source file
# @local f        Formatted result of the compiled arg/env/set information
# @local t        Formatted entry in the table of contents
# @local a        Iterator in function's arguments
# @local _a       Formatted arg/set/env iterator output
function add_fn(name, line,   f,t,a,_a) {
   # Internal or no annotations, nothing to do.
   if (empty(fn))      return
   if (fn["internal"]) return

   # INFO if @type or @section attached to a function.
   if (typedef) typedef_in_function(line)
   if (section) section_in_function(line)

   # ERROR if both @noargs and @arg
   if (fn["noargs"] && !empty(fn["args"])) {
      noargs_args_mismatch(line)
   }

   # Function heading/location.
   f = _gensub(name, md["fn"]) "()"
   f = cat(f, "(*ln. "  FNR  ", in "  FILENAME  "*)\n")
   
   # Description.
   if (buffer) {
      f = cat(f, buffer)
   }

   # Args.
   if (fn["args"][0]) {
      f = cat(f "\n", _gensub("args", md["sub"]))
      f = cat(f, "|name|type|desc|")
      f = cat(f, "|:---|:---|:---|")

      for (a in fn["args"]) {
         f = cat(f, fn["args"][a])
      }
   }

   # Sets global variables.
   if (fn["set"][0]) {
      f = cat(f "\n", _gensub("set", md["sub"]))
      for (a in fn["set"]) {
         _a = fn["set"][a]
         _SET[_a][name] = true

         _a = _gensub("`" _a "`", md["li1"])
         f = cat(f, _a)
      }
   }

   # Uses global/environment variables.
   if (fn["env"][0]) {
      f = cat(f "\n", _gensub("uses", md["sub"]))
      for (a in fn["env"]) {
         _a = fn["env"][a]
         _ENV[_a][name] = true

         _a = _gensub("`" _a "`", md["li1"])
         f = cat(f, _a)
      }
   }

   # References to other variables.
   if (fn["see"][0]) {
      f = cat(f "\n", _gensub("see", md["sub"]))
      for (a in fn["see"]) {
         _a = link(fn["see"][a], "monospace")
         _a = gensub(".*", md["li1"],  1, _a)
         f = cat(f, _a)
      }
   }

   # Append to TOC.
   t = gensub(".*", md["li2"], 1, link(name, "monospace"))

   _TOC  = cat(_TOC,  t)
   _BODY = cat(_BODY, f "\n")
}


# build_env()
# @description
#  Compiles from _ENV a list of global variables, and which functions reference
#  each of them.
#
# @env   _ENV
#
# @local out      Formatted H1 heading and variable/function list
# @local v        Iterator variable name
# @local fn       Iterator function name
function build_env(   out,v,fn) {
   out = _gensub("Variables referenced.", md["h1"])

   for (v in _ENV) {
      out = cat(out, gensub(".*", md["li1"], 1, "`" v "`"))
      for (fn in _ENV[v]) {
         fn  = link(fn, "monospace")
         out = cat(out, gensub(".*", md["li2"], 1, fn))
      }
   }

   return out "\n"
}


# build_set()
# @description
#  Compiles from _SET a list of global variables, and which functions set each
#  of them.
#
# @env   _SET
#
# @local out      Formatted H1 heading and variable/function list
# @local v        Iterator variable name
# @local fn       Iterator function name
function build_set(   out,v,fn) {
   out = _gensub("Variables set.", md["h1"])

   for (v in _SET) {
      out = cat(out, gensub(".*", md["li1"], 1, "`" v "`"))
      for (fn in _SET[v]) {
         fn  = link(fn, "monospace")
         out = cat(out, gensub(".*", md["li2"], 1, fn))
      }
   }

   return out "\n"
}


#-------------------------------------------------------------------------------
# @section                          matches

# If hit an error, skip until a synchronization point--a valid @-identifier.
panicking && /^\s*#/ {
   if (opts[$2]) {
      panicking = false
   } else {
      next
   }
}


# match:  @section
#
# Requires a section name (may contain multiple words).
/^\s*#\s+@section(\s*|$)/ {
   line = $0

   sub(/^\s*#\s+@section\s*/, "")
   section = $0

   if (!section) {
      log_error(line, "Missing section title", "ERROR")
   }

   buffer_open = false
   next
}


# match:  @arg
#
# Requires an argument name, optionally a colon-prefixed type, and one-line
# description.
/^\s*#\s+@arg\s+/ {
   line = $0

   sub(/^\s*#\s+@arg\s+/, "")
   name = shift()

   if (!name) {
      log_error(line, "Missing argument name")
      next
   }

   if (match($1, /^:[[:alpha:]_][[:alnum:]_:-]*/)) {
      sub(/^:/, "")
      type = shift()
   } else {
      type = ""
   }

   add_arg(name, type, $0)

   buffer_open = false
   next
}


# match:  @set
#
/^\s*#\s+@set\s+/ {
   sub(/^\s*#\s+@set\s+/, "")
   push(fn["set"], shift())

   buffer_open = false
   next
}


# match:  @env
#
/^\s*#\s+@env\s+/ {
   sub(/^\s*#\s+@env\s+/, "")
   push(fn["env"], shift())

   buffer_open = false
   next
}


# match:  @see
#
/^\s*#\s+@see\s+/ {
   sub(/^\s*#\s+@see\s+/, "")
   push(fn["see"], shift())

   buffer_open = false
   next
}


# match:  @internal
#
# Do not include this function, or its docs, in the generated output.
/^\s*#\s+@internal(\s+|$)/ {
   fn["internal"] = true
   next
}


# match:  @noargs
#
# Doesn't actually impact the output, just use for error reporting if an @arg
# is additionally passed.
/^\s*#\s+@noargs(\s+|$)/ {
   fn["noargs"] = true
   next
}


# match:  @description
#
# Will match text beginning on the same line, as well as the subsequent line.
# Dedents text to the level of the initial line.
match($0, /^\s*#\s+@description(\s+|$)/) {
   sub(/^\s*#\s+@description\s*/, "")
   if ($1) {
      column = RLENGTH
      buffer = $0
   }

   buffer_open = true
   next
}


# match:  @type
#
# Declares the following variable as a "type". Easier way of tracking down where
# where things have been defined.
/^\s*#\s+@type(\s+|$)/ {
   buffer_open = false
   typedef     = true
   next
}


# match:  comment line with no text
#
/^\s*#\s*$/ && buffer_open {
   buffer = buffer "\n"
   next
}


# match:  regular comment text, potentially add to buffer
#
/^\s*#/ && buffer_open {
   if (!column) {
      match($0, /^\s*#\s+/)
      column = RLENGTH
   }

   if (match($0, /^\s*#\s*/) && (column > RLENGTH)) {
      buffer_open = false
   } else {
      sub(".{" column "}", "")
      buffer = cat(buffer, $0)
   }

   next
}


# match:  unknown @-identifiers
#
/^\s*#\s+@[[:alpha:]]/ {
   line = $0
   sub(/^\s*#\s+@/, "")
   write(line, "Unknown @-flag: " shift(), "INFO")
}


#-------------------------------------------------------------------------------
# @section                      synchronization
# @description
#  Confusingly enough, I am using "synchronization" here differently from in the
#  error panicking context. These are the points at which to synchronize all the
#  accumulated function/section data and dump the compiled output.
#
#  Synchronization points (all non-comment text):
#  * function declarations
#  * variable declarations
#  * non-comment text after a section (and description?) flag

# match:  function declarations, both styles
#
/^\s*function\s+[[:alpha:]_][[:alnum:]_:-]*/ ||
/^\s*[[:alpha:]_][[:alnum:]_:-]*\s*\(\s*\)/ {
   line = $0

   sub(/^\s*function/, "")
   sub(/\s*/, "")
   sub(/\(.*)/, "")

   add_fn($1, line)
   reset() ; next
}


# match:  variable declarations, for typedefs
#
typedef && (/^\s*(declare|local)/ || /\s*[[:alpha:]_][[:alnum:]_]*=/) {
   sub(/^\s*)/, "")
   sub(/^\s*(declare|local)(\s+\-(\-|[gAainxr]+))?\s*/, "")
   sub(/=.*/, "")

   add_type($1)
   typedef = false

   reset() ; next
}


# match:  non-comment text or empty lines
#         syncs section & description
#
!/^\s*#/ || /^\s*$/ {
   if (section) add_section()
   typedef = false
   reset() ; next
}


END {
   if (!empty(_ERRORS)) {
      for (idx in _ERRORS) {
         print _ERRORS[idx] > "/dev/stderr"
      }
      exit 1
   }

   print _TOC

   if (_TYPE) _TYPE = "\n" _TYPE "\n"
   print _TYPE

   if (!empty(_ENV)) print build_env()
   if (!empty(_SET)) print build_set()

   print _BODY
   print "<br/><br/>\n:: *generated by @carlinigraphy/awkdoc* ::"
}
